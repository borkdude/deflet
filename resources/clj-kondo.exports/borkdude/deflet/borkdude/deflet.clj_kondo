(ns borkdude.deflet
  (:require [clj-kondo.hooks-api :as hooks-api]))

(defn deflet* [children]
  (let [f (first children)
        r (next children)
        lnode? (hooks-api/list-node? f)
        call-node (first (:children f))
        call-sym (when lnode? (hooks-api/sexpr call-node))]
    (if (#{'def 'defp 'bind 'bindp} call-sym)
      (let [def-children (:children f)]
        (with-meta (hooks-api/list-node
                    [(hooks-api/coerce 'clojure.core/let)
                     (hooks-api/vector-node
                      (cond-> [(second def-children)
                               (nth def-children 2)]
                        (#{'bind 'bindp} call-sym)
                        (conj (hooks-api/token-node '_) call-node)))
                     (deflet* r)])
          (meta f)))
      (if-not r (or f (hooks-api/coerce nil))
              (with-meta
                (hooks-api/list-node (list (hooks-api/coerce 'do)
                                           f
                                           (deflet* r)))
                (meta f))))))

(defn deflet [{:keys [node]}]
  (let [children (:children node)
        new-node (deflet* children)]
    {:node new-node}))
